import cuchaz.enigma.command.ConvertMappingsCommand

buildscript {
	repositories {
		maven {
			name "Fabric Repository"
			url 'https://maven.fabricmc.net'
		}
	}

	dependencies {
		classpath "cuchaz:enigma:0.14.2.138"
	}
}

plugins {
	id 'maven'
	id 'maven-publish'
}

def localMappingsPath = "$buildDir/v2Mappings"
new File(localMappingsPath).mkdirs()
file('mappings').eachFile {
	publishMapping(it, false, localMappingsPath)
}

// Old intermediary
file('legacy/mappings').eachFile {
	publishMapping(it, true, localMappingsPath)
}

def publishMapping(File it, boolean old, def localMappingsPath) {
	if (!it.name.endsWith(".tiny")) return

	def mcVer = it.name.replace(".tiny", "")

	File v1MappingFile = it
	File v2MappingFile = new File("$localMappingsPath/${it.name}")

	def conversionTask = "convert${it.name}ToV2" + (old ? "old" : "new")
	tasks.register(conversionTask) {
		group = "V2 Conversion"
		inputs.file(v1MappingFile)
		outputs.file(v2MappingFile)

		doLast {
			new ConvertMappingsCommand().run(
					"tiny",
					v1MappingFile.path,
					"tinyv2:official:intermediary",
					v2MappingFile.path
			)
		}
	}

	Jar makeV1Jar = makeJar(mcVer, v1MappingFile, false, old)
	Jar makeV2Jar = makeJar(mcVer, v2MappingFile, true, old)


	build.dependsOn makeV1Jar
	build.dependsOn makeV2Jar

	makeV2Jar.dependsOn conversionTask

	publishing {
		publications {
			create("${mcVer.replace(" ", "")}_mavenJava" + (old ? "_old" : ""), MavenPublication) {
				groupId 'net.legacyfabric'
				artifactId !old ? "intermediary" : "intermediary-old"
				version mcVer
				artifact(makeV1Jar.archiveFile) {
					builtBy makeV1Jar
				}
				artifact(makeV2Jar.archiveFile) {
					builtBy makeV2Jar
					classifier = "v2"
				}
			}
		}
	}
}

def makeJar(String mcVersion, File mappings, boolean v2, boolean old) {
	def jarFilename = "intermediary2-" + mcVersion + (v2 ? "-v2" : "") + (old ? "-old" : "")
	return task("${mcVersion}_makeJar" + (v2 ? "v2" : "") + (old ? "_old" : ""), type: Jar) {
		baseName jarFilename
		from(file(mappings)) {
			into "mappings"
			rename mappings.name, "mappings.tiny"
		}
		destinationDirectory = file("build/jars")
	}
}

def ENV = System.getenv()

publishing {
	repositories {
        if (ENV.MAVEN_PUBLISH_CREDENTIALS) {
            maven {
                url "https://repo.legacyfabric.net/repository/legacyfabric"
                credentials {
                    username ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[0]
                    password ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[1]
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
}
}
