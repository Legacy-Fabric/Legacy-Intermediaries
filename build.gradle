import cuchaz.enigma.command.ConvertMappingsCommand

buildscript {
	repositories {
		maven {
			name "Fabric Repository"
			url 'https://maven.fabricmc.net'
		}
	}

	dependencies {
		classpath "cuchaz:enigma:0.14.2.138"
	}
}

plugins {
	id 'maven'
	id 'maven-publish'
}

static def buildTime() {
	def df = new java.text.SimpleDateFormat("yyyyMMddHHmm")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))
	return df.format(new Date())
}

def build_time = build_time();

def localMappingsPath = "$buildDir/v2Mappings"
new File(localMappingsPath).mkdirs()
file('mappings').eachFile {
	if (!it.name.endsWith(".tiny")) return

	def mcVer = it.name.replace(".tiny", "")

	File v1MappingFile = it
	File v2MappingFile = new File("$localMappingsPath/${it.name}")

	def conversionTask = "convert${it.name}ToV2"
	tasks.register(conversionTask) {
		group = "V2 Conversion"
		inputs.file(v1MappingFile)
		outputs.file(v2MappingFile)

		doLast {
			new ConvertMappingsCommand().run(
					"tiny",
					v1MappingFile.path,
					"tinyv2:official:intermediary",
					v2MappingFile.path
			)
		}
	}

	Jar makeV1Jar = makeJar(mcVer, v1MappingFile, false)
	Jar makeV2Jar = makeJar(mcVer, v2MappingFile, true)

	def mcVerTimed = mcVer + "+" + build_time

	Jar makeV1JarTimed = makeJar(mcVerTimed, v1MappingFile, false)
	Jar makeV2JarTimed = makeJar(mcVerTimed, v2MappingFile, true)

	build.dependsOn makeV1Jar
	build.dependsOn makeV2Jar
	build.dependsOn makeV1JarTimed
	build.dependsOn makeV2JarTimed

	makeV2Jar.dependsOn conversionTask

	publishing {
		publications {
			create("${mcVer.replace(" ", "")}_mavenJava", MavenPublication) {
				groupId 'net.fabricmc'
				artifactId "intermediary"
				version mcVer
				artifact(makeV1Jar.archiveFile) {
					builtBy makeV1Jar
				}
				artifact(makeV2Jar.archiveFile) {
					builtBy makeV2Jar
					classifier = "v2"
				}
			}

			create("${mcVer.replace(" ", "")}_timed_mavenJava", MavenPublication) {
				groupId 'net.fabricmc'
				artifactId "intermediary"
				version mcVerTimed
				artifact(makeV1JarTimed.archiveFile) {
					builtBy makeV1JarTimed
				}
				artifact(makeV2JarTimed.archiveFile) {
					builtBy makeV2JarTimed
					classifier = "v2"
				}
			}
		}
	}

}

def makeJar(String mcVersion, File mappings, boolean v2) {
	def jarFilename = "intermediary2-" + mcVersion + (v2 ? "-v2" : "")
	return task("${mcVersion}_makeJar" + (v2 ? "v2" : ""), type: Jar) {
		baseName jarFilename
		from(file(mappings)) {
			into "mappings"
			rename mappings.name, "mappings.tiny"
		}
		destinationDirectory = file("build/jars")
	}
}

def ENV = System.getenv()

publishing {
	repositories {
		if (ENV.MAVEN_PUBLISH_TOKEN) {
			maven {
				url 'https://maven.legacyfabric.net/'
				credentials {
					username 'BobTheBuildSlave'
					password ENV.MAVEN_PUBLISH_TOKEN
				}
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
	}
}
