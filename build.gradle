buildscript {
	repositories {
		maven {
			name "Fabric Repository"
			url 'https://maven.fabricmc.net'
		}
	}

	dependencies {
		classpath "net.fabricmc:mapping-io:0.3.0"
	}
}

plugins {
	id 'maven-publish'
}

import net.fabricmc.mappingio.MappingReader
import net.fabricmc.mappingio.MappingWriter
import net.fabricmc.mappingio.format.MappingFormat
import net.fabricmc.mappingio.adapter.MappingNsCompleter

tasks.register("build")

def ENV = System.getenv()

def localMappingsPath = "$buildDir/v2Mappings"
new File(localMappingsPath).mkdirs()
file('mappings').eachFile {
	if (!it.name.endsWith(".tiny")) return

	def mcVer = it.name.replace(".tiny", "")

	File v1MappingFile = it
	File v2MappingFile = new File("$localMappingsPath/${it.name}")

	def conversionTask = tasks.register("convert${it.name}ToV2") {
		group = "V2 Conversion"
		inputs.file(v1MappingFile)
		outputs.file(v2MappingFile)

		doLast {
			MappingWriter.create(v2MappingFile.toPath(), MappingFormat.TINY_2).withCloseable {
				def visitor = new MappingNsCompleter(it, ["intermediary": "official"], true)
				MappingReader.read(v1MappingFile.toPath(), visitor)
			}
		}
	}

	def makeV1Jar = makeJar(mcVer, v1MappingFile, false)
	def makeV2Jar = makeJar(mcVer, v2MappingFile, true)
	makeV2Jar.configure {
		dependsOn conversionTask
	}

	project.logger.lifecycle("Publishing ${mcVer}")

	build.dependsOn makeV1Jar
	build.dependsOn makeV2Jar

	publishing {
		publications {
			create("${mcVer.replace(" ", "")}_mavenJava", MavenPublication) {
				groupId "net.legacyfabric.v$project.intermediaryVersion"
				artifactId "intermediary"
				version mcVer
				artifact(makeV1Jar.get().archiveFile) {
					builtBy makeV1Jar
				}
				artifact(makeV2Jar.get().archiveFile) {
					builtBy makeV2Jar
					classifier = "v2"
				}
			}
		}
	}
}

def makeJar(String mcVersion, File mappings, boolean v2) {
	def jarFilename = "intermediary-" + mcVersion + (v2 ? "-v2" : "") + ".jar"
	return tasks.register("${mcVersion}_makeJar" + (v2 ? "v2" : ""), Jar) {
		archiveFileName = jarFilename
		group = "jar"
		from(file(mappings)) {
			into "mappings"
			rename mappings.name, "mappings.tiny"
		}
		destinationDirectory = file("build/jars")
		manifest {
			attributes(
					"Intermediary-Version": project.intermediaryVersion,
			)
		}
	}
}


publishing {
	repositories {
        if (ENV.MAVEN_PUBLISH_CREDENTIALS) {
            maven {
                url "https://repo.legacyfabric.net/repository/legacyfabric"
                credentials {
                    username ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[0]
                    password ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[1]
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
	}
}
